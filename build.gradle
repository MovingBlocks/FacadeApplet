// This facade is for running the game as an applet

// Grab all the common stuff like plugins to use, artifact repositories, code analysis config
apply from: "$rootDir/config/gradle/common.gradle"

import org.apache.tools.ant.filters.FixCrLfFilter;
import org.apache.tools.ant.taskdefs.condition.IsSigned;

import java.util.concurrent.*

def LWJGL_VERSION = '2.9.1'
def JINPUT_VERSION = '2.0.5'

// Declare "extra properties" (variables) for the project - a Gradle thing that makes them special.
ext {
    // Project paths
    subDirLibs = 'libs'
    subDirModules = 'mods'
    destDirApplet = 'distributions/applet'
    jarFileName = 'Terasology.jar'
    templatesDir = file('templates')

    // Read environment variables, including variables passed by jenkins continuous integration server
    env = System.getenv()
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Applet
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

configurations {
    applet
    modules {
        description = 'Modules without deps'
        transitive = false
    }
}

dependencies {
    compile project(':engine')
    applet (group: 'org.lwjgl.lwjgl', name: 'lwjgl_util_applet', version: LWJGL_VERSION)
    applet (group: 'org.terasology.engine', name: 'engine', version: '+')
    modules (group: 'org.terasology.modules', name: 'Core', version: '+')
}

jar {
   manifest.mainAttributes('Permissions': 'all-permissions')
   manifest.mainAttributes('Codebase': '*')
   manifest.mainAttributes('Application-Name': 'Terasology Applet')
   manifest.mainAttributes('Application-Library-Allowable-Codebase': '*')
   manifest.mainAttributes('Caller-Allowable-Codebase': '*')
   manifest.mainAttributes('Trusted-Only': 'false')
}

// Distribute modules - with the option to provide a list of additional modules to include as dependencies
// Example command including additional modules: gradlew -PextraModules="Signalling,BlockNetwork"
task addModules () {
    description = "Prepares modules for distribution"

    // If the execution was supplied with an extraModules property then include those modules as well
    if (project.hasProperty('extraModules')) {
        extraModules.split(',').each { String dependency ->
            logger.lifecycle('Extra module: ' + dependency)
            dependencies  {
                modules (group: 'org.terasology.modules', name: dependency, version: '+')
            }
        }
    }
}

task syncIntoApplet(type:Sync) {
    description = "Copies all relevant files to the applet folder"
    
    from ("$rootDir/README.markdown") {
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance("crlf"))
        rename('README.markdown', 'README')
    }
    from ("$rootDir/LICENSE") {
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance("crlf"))
    }
    from ("$rootDir/NOTICE") {
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance("crlf"))
    }
    from jar                              // this is the applet.jar file
    into("$buildDir/$destDirApplet")
    
    into(subDirLibs) {
        from configurations.applet        // these are all applet deps.
    }
    into(subDirModules) {
        from configurations.modules
    }
}

task createIndexHtml(type:Copy) {
    mustRunAfter syncIntoApplet

    // add all libs to classpath
    def jars = []
    configurations.applet.each {
        jars += subDirLibs + '/' + it.getName()
    }
    
    def modules = []
    configurations.modules.each {
        modules += it.getName()
    }
    
    from('.')
    into("$buildDir/$destDirApplet")
    include 'index.html'
    expand(lwjglVersion: LWJGL_VERSION, jinputVersion : JINPUT_VERSION, jars : jars.join(", "), modules : modules.join(", "))
}


task distApplet {
    description = "Creates an Applet folder"
    
    dependsOn addModules
    dependsOn syncIntoApplet
    dependsOn createIndexHtml
}

// At the end of the applet distribution sign the jar files so they can get expanded rights in a browser
distApplet.doLast {

    if (!file('certum.jks').exists()) {
        throw new GradleException("Copy the \"certum.jks\" keystore file in \"$projectDir\" first. See the README file on how to create the keystore.");
    }

    FileTree tree = fileTree(dir: "$buildDir/$destDirApplet")
    
    tree.include '*.jar'
    tree.include "$subDirLibs/*.jar"
    tree.include "$subDirModules/*.jar"
    
    def pool = Executors.newCachedThreadPool()
    
    tree.each {
        // TODO: Remove this part as soon as LWJGL applet jars work correctly
        if (it.getName().toLowerCase().startsWith('lwjgl')) {
            logger.lifecycle("Re-signing invalid LWJGL file $it");
            fixBrokenLwjglJar(it);
            pool.submit({ sign(v) });
        }
        else if (!IsSigned.isSigned(it, null)) {
            final File v = it;
            pool.submit({ sign(v) });
        } else {
            logger.lifecycle("File $it already signed");
        }
    }
    
    pool.shutdown()
    pool.awaitTermination(10, TimeUnit.MINUTES)
    logger.lifecycle("Signing complete");
}


def sign(File it) {
    logger.lifecycle("Signing $it");
    ant.signjar(jar: it, keystore: "certum.jks", alias: 'cervator', storepass: 'Terasology', tsaurl: 'http://time.certum.pl/')
}

// Overwrite invalid AppLibAC attribute ("true") in LWJGL 2.9.1 jars
def fixBrokenLwjglJar(File jar) {
    ant.jar(destfile: jar, update: true) {
        delegate.manifest {
            attribute(name: 'Application-Library-Allowable-Codebase', value: '*')
        }
    }
}



task distAppletZip(type:Zip) {
    description = "Creates an Applet ZIP file from '$buildDir/$destDirApplet'"
    dependsOn distApplet
    //dependsOn createVersionInfoFile
    //dependsOn createVersionFile
    appendix 'Applet'
    from "$buildDir/$destDirApplet"
    //from "$buildDir/$versionFileName"
}
